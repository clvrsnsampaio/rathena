//===== rAthena Script =======================================
//= Eden Group Quests - Mission [41 - 55]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 41 - 55.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.4 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,40,38,3	script	Mission [41 - 55]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Você não é um membro do Grupo do Éden. Você não está qualificado para acessar o quadro de avisos.";
		close;
	}
	if (checkweight(1201,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Espere um segundo!!";
		mes "Você não pode receber mais itens porque está carregando muitos itens. Por favor, tente novamente depois de aliviar o peso.^000000";
		close;
	}
	if (checkquest(12088) == -1) {
		if (BaseLevel < 41 || BaseLevel > 55) {
			mes "Quadro de missões para aventureiros de Nv41~55.";
			close;
		}
		mes "[Quadro de Missões]";
		mes "Quadro de avisos de missões.";
		next;
		if(select("Verificar as Missões.:Cancelar.") == 2) {
			mes "^000077Ok. Vamos verificar as missões na próxima vez.^000000.";
			close;
		}
		switch(select("Caçar Sapo de Thara:Caçar Soldadinho de Chumbo:Caçar Kukre:Caçar Filhote de Orc:Caçar Guerreiro Orc:Caçar Vadon:Caçar Megalodon:Caçar Molusco:Caçar Cornutus:Caçar Myst:Caçar Ostra:Caçar Esfera Marinha:Caçar Fen:Caçar Esqueleto Orc:Caçar Zenorc:Caçar Múmia")) {
			case 1: callsub L_Quest,12072,"Você já viu um sapo vermelho? É chamado de Sapo de Thara e é muito raro. Estamos tentando verificar onde ele habita. Então, por favor, cace 20 Sapos de Thara.";
			case 2: callsub L_Quest,12073,"Ouvi falar de alguns brinquedos atacando pessoas. É ridículo! Vamos mostrar a eles nosso poder! Temos que caçar 15 Soldadinhos de Chumbo!";
			case 3: callsub L_Quest,12074,"Alguém foi roubado perto da praia! O possível suspeito é um monstro criminoso chamado Kukre. Por favor, cace 30 Kukres.";
			case 4: callsub L_Quest,12075,"Filhotes de Orc estão ficando mais violentos hoje em dia. Por favor, cace 15 Filhotes de Orc.";
			case 5: callsub L_Quest,12076,"Guerreiros Orc estão aumentando sua agressividade. Por favor, cace 20 Guerreiros Orc.";
			case 6: callsub L_Quest,12077,"Você já viu caranguejos com cascas grossas? Eles são Vadons. Por favor, cace 15 Vadons.";
			case 7: callsub L_Quest,12078,"Peixes mortos estão por toda a praia e está piorando. Por favor, cace 30 Megalodons.";
			case 8: callsub L_Quest,12079,"Você pode caçar Moluscos? Preciso de 15 Moluscos para meu estudo. Por favor, me ajude.";
			case 9: callsub L_Quest,12080,"Um Cornutus idiota mordeu o pé do Prefeito. O Prefeito está chateado, então precisamos caçar 20 Cornutus para deixá-lo feliz!";
			case 10: callsub L_Quest,12081,"As pessoas estão surtando por causa de Myst. Parece um fantasma, então assusta as pessoas. Por favor, cace 15 Mysts.";
			case 11: callsub L_Quest,12082,"Não pergunte o porquê, mas por favor, cace 30 Ostras. É uma missão secreta!";
			case 12: callsub L_Quest,12083,"Esfera Marinha? Você sabe o que é uma Esfera Marinha? Tenho uma missão para você caçar 15 Esferas Marinhas, então, por favor, faça isso.";
			case 13: callsub L_Quest,12084,"O prefeito quer comer um prato de peixe delicioso. Fen será o ingrediente perfeito para isso. Por favor, cace 20 Fens!";
			case 14: callsub L_Quest,12085,"Monstros Orc mortos voltaram para nossa cidade e reviveram como Esqueletos Orc. Estamos no caos! Por favor, cace 15 Esqueletos Orc.";
			case 15: callsub L_Quest,12086,"Alguns monstros estranhos chamados Zenorc estão nos atacando. Por favor, cace 30 Zenorcs.";
			case 16: callsub L_Quest,12087,"Esteja preparado! É uma missão cruel! Cace 15 Múmias!! Eu tentei uma vez, mas não consegui. É muito assustador!";
		}
		end;
	}
	setarray .@Exp[0],9600,7200,15750,7875,11100,8550,17100,9000,12300,9900,20250,10350,13800,11025,22500,11250;
	for(set .@i,0; .@i<16; set .@i,.@i+1)
		if (checkquest(12072+.@i,HUNTING) == 2) {
			mes "[Quadro de Missões]";
			mes "Você completou a missão.";
			mes "Aqui está sua recompensa.";
			erasequest 12088;
			erasequest 12072+.@i;
			specialeffect2 EF_STEAL;
			getexp .@Exp[.@i],0;
			close;
		}
	if ((checkquest(12072,HUNTING) == 0) && (checkquest(12073,HUNTING) == 0) && (checkquest(12074,HUNTING) == 0) && (checkquest(12075,HUNTING) == 0) && (checkquest(12076,HUNTING) == 0) && (checkquest(12077,HUNTING) == 0) && (checkquest(12078,HUNTING) == 0) && (checkquest(12079,HUNTING) == 0) && (checkquest(12080,HUNTING) == 0) && (checkquest(12081,HUNTING) == 0) && (checkquest(12082,HUNTING) == 0) && (checkquest(12083,HUNTING) == 0) && (checkquest(12084,HUNTING) == 0) && (checkquest(12085,HUNTING) == 0) && (checkquest(12086,HUNTING) == 0) && (checkquest(12087,HUNTING) == 0)) {
		mes "Agora você pode ver o quadro de avisos.";
		erasequest 12088;
		close;
	}
	mes "Não há nada de especial para fazer.";
	close;
L_Quest:
	mes "[Nota do Quadro de Missões]";
	mes getarg(1);
	next;
	if(select("Aceitar a missão.:Cancelar.") == 1) {
		mes "[Quadro de Missões]";
		mes "Você aceitou a missão. Para mais detalhes, pressione as teclas 'alt + u', você pode obter informações na Janela de Missões.";
		setquest 12088;
		setquest getarg(0);
	}
	close;
}
