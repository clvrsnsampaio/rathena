//===== rAthena Script =======================================
//= Eden Group Quests - Mission [71 - 85]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 71 - 85.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official scripts. [Euphy]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,44,38,3	script	Mission [71 - 85]	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Apenas membros do -";
		mes "- Grupo do Éden podem ler -";
		mes "- este quadro de avisos. -";
		close;
	}

	// Clear the original quest data (no longer used).
	if (slv_quest) set slv_quest,0;
	if (checkquest(10102) > -1 || checkquest(10103) > -1 || checkquest(10104) > -1 || checkquest(10105) > -1 || checkquest(10106) > -1) {
		for(set .@quest,10102; .@quest<=10106; set .@quest,.@quest+1) {
			if (checkquest(.@quest) > -1)
				erasequest .@quest;
		}
		mes "- Como o cliente se mudou -";
		mes "- para outro lugar, -";
		mes "- o formulário de inscrição foi removido. -";
		next;
	}

	if (BaseLevel < 71) {
		mes "- Apenas membros do -";
		mes "- nível correto podem ler -";
		mes "- este quadro de avisos. -";
		mes "- Tente encontrar um quadro -";
		mes "- que se adeque ao seu nível. -";
		close;
	} else if (BaseLevel > 85) {
		// Quest IDs: 10107-10123, 5055-5057
		setarray .@quests[0],
			10107,10108,10109,
			10110,10111,10112,10113,
			10114,10115,10116,10117,
			10118,10119,10120,
			10121,10122,10123,5055,5056;
		for(set .@i,0; .@i<getarraysize(.@quests); set .@i,.@i+1) {
			if (checkquest(.@quests[.@i],HUNTING) == 2)
				set .@complete[getarraysize(.@complete)], .@quests[.@i];
		}
		if (checkquest(5057) > -1 && countitem(7187) >= 30)
			set .@complete[getarraysize(.@complete)],5057;
		if (getarraysize(.@complete)) {
			mes "Você tem missões em andamento.";
			mes "Você quer entregá-las?";
			next;
			if(select("Claro.:Não.") == 2)
				close;
			for(set .@i,0; .@i<getarraysize(.@complete); set .@i,.@i+1)
				callsub L_Quest,.@complete[.@i];
			close;
		}
		mes "- Apenas membros do -";
		mes "- nível correto podem ler -";
		mes "- este quadro de avisos. -";
		mes "- Tente encontrar um quadro -";
		mes "- que se adeque ao seu nível. -";
		close;
	}
	mes "- Escolha onde você -";
	mes "- gostaria de caçar. -";
	next;
	switch(select("Calabouço da Esfinge:Glast Heim:Área de Juno:Torre do Relógio:Ilhas Localizadas")) {
	case 1:
		mes "- Existem vários pedidos -";
		mes "- relacionados ao Calabouço da Esfinge -";
		mes "- neste Quadro de Missões. -";
		next;
		set .@str1$,"Os moradores de Morroc estão preocupados com o número crescente de monstros no Calabouço da Esfinge. ";
		set .@str2$,"- Meidi de Morroc -";
		switch(select("Caçar Réquiem:Caçar Marduk:Caçar Pasana")) {
			case 1: callsub L_Quest,10107,.@str1$+"Cace 10 Réquiems.",.@str2$;
			case 2: callsub L_Quest,10108,.@str1$+"Cace 10 Marduks.",.@str2$;
			case 3: callsub L_Quest,10109,.@str1$+"Cace 10 Pasanas.",.@str2$;
		}
	case 2:
		mes "- Existem vários pedidos -";
		mes "- relacionados a Glast Heim -";
		mes "- neste Quadro de Missões. -";
		next;
		set .@str1$,"Tenho muito medo de que os monstros de Glast Heim nos ataquem. ";
		set .@str2$,"- Ancellia de Geffen -";
		switch(select("Caçar Moldura Maldita:Caçar Druida Maligno:Caçar Aparição:Caçar Raydric Arqueiro")) {
			case 1: callsub L_Quest,10110,.@str1$+"Cace 10 Molduras Malditas.",.@str2$;
			case 2: callsub L_Quest,10111,.@str1$+"Cace 10 Druidas Malignos.",.@str2$;
			case 3: callsub L_Quest,10112,.@str1$+"Cace 10 Aparições.",.@str2$;
			case 4: callsub L_Quest,10113,.@str1$+"Cace 10 Raydrics Arqueiros.",.@str2$;
		}
	case 3:
		mes "- Existem vários pedidos -";
		mes "- relacionados à área -";
		mes "- ao redor de Juno. -";
		next;
		set .@str1$,"Aqueles que não podem lutar, como eu, acham muito mais difícil viajar devido ao grande número de monstros. ";
		set .@str2$,"- Jeanbai, viajante -";
		switch(select("Caçar Grand Peco:Caçar Dorminhoco:Caçar Bode:Caçar Harpia")) {
			case 1: callsub L_Quest,10114,.@str1$+"Cace 20 Grand Pecos.",.@str2$;
			case 2: callsub L_Quest,10115,.@str1$+"Cace 20 Dorminhocos.",.@str2$;
			case 3: callsub L_Quest,10116,.@str1$+"Cace 20 Bodes.",.@str2$;
			case 4: callsub L_Quest,10117,.@str1$+"Cace 20 Harpias.",.@str2$;
		}
	case 4:
		mes "- Existem vários pedidos -";
		mes "- relacionados à Torre do Relógio -";
		mes "- neste Quadro de Missões. -";
		next;
		set .@str1$,"Eu adorava ouvir a doce melodia da Torre do Relógio de Al De Baran, mas não consigo ouvir mais nada por causa de todos os barulhos que os monstros estão fazendo. ";
		set .@str2$,"- Rizingsetter, Presidente da comunidade Amor ao Relógio -";
		switch(select("Caçar Relógio:Caçar Punk:Caçar Rideword")) {
			case 1: callsub L_Quest,10118,.@str1$+"Cace 15 Relógios antes que quebre.",.@str2$;
			case 2: callsub L_Quest,10119,.@str1$+"Cace 15 Punks antes que quebre.",.@str2$;
			case 3: callsub L_Quest,10120,.@str1$+"Cace 15 Ridewords antes que quebre.",.@str2$;
		}
	case 5:
		mes "- Existem vários pedidos -";
		mes "- relacionados às Ilhas Localizadas -";
		mes "- neste Quadro de Missões. -";
		next;
		set .@str1$,"Quero ir em uma aventura, mas há tantas coisas com as quais tenho que lidar. Se eu não posso, então os outros também não podem! ";
		set .@str2$,"- Funfy, que quer ir em uma aventura -";
		switch(select("Caçar Kikimora:Caçar Boneca de Miyabi:Caçar Mi Gao:Caçar Mula Sem Cabeça:Caçar Tamruan:Coletar Máscaras do Festival para o Festival")) {
			case 1: callsub L_Quest,10121,.@str1$+"Cace 15 Kikimoras em Moscovia!",.@str2$;
			case 2: callsub L_Quest,10122,.@str1$+"Cace 15 Bonecas de Miyabi em Amatsu!",.@str2$;
			case 3: callsub L_Quest,10123,.@str1$+"Cace 15 Mi Gaos em Luoyang!",.@str2$;
			case 4: callsub L_Quest,5055,"Eu sei que existem muitos monstros aterrorizantes fora da cidade, mas o que me aterroriza são essas Mulas Sem Cabeça espreitando por aí. Espero que elas nunca se aventurem na cidade. Cace 15 Mulas Sem Cabeça!","- Mariana de Brasilis -";
			case 5: callsub L_Quest,5056,"Tamruans fazem muito barulho. Eu não me importo de manhã, mas eles me impedem de dormir à noite. Eu quero dormir. Cace 15 Tamruans!","- Xamã Pumo de Ayothaya -";
			case 6: callsub L_Quest,5057,"As Crianças Bolinho roubaram todas as máscaras que eu havia preparado para o festival. Tente conseguir 30 máscaras! Eu te devo uma~","- SaYumMoon Chefe de Kunlun -";
		}
	}
	end;

//callsub L_Quest,<quest ID>{,"<description1>","<description2>"};
L_Quest:
	set .@quest, getarg(0);
	switch(.@quest) {
		case 10107: set .@name$,"Caçar Réquiem"; set .@exp,15000; break;
		case 10108: set .@name$,"Caçar Marduk"; set .@exp,15000; break;
		case 10109: set .@name$,"Caçar Pasana"; set .@exp,15000; break;
		case 10110: set .@name$,"Caçar Moldura Maldita"; set .@exp,20000; break;
		case 10111: set .@name$,"Caçar Druida Maligno"; set .@exp,20000; break;
		case 10112: set .@name$,"Caçar Aparição"; set .@exp,20000; break;
		case 10113: set .@name$,"Caçar Raydric Arqueiro"; set .@exp,20000; break;
		case 10114: set .@name$,"Caçar Grand Peco"; set .@exp,30000; break;
		case 10115: set .@name$,"Caçar Dorminhoco"; set .@exp,30000; break;
		case 10116: set .@name$,"Caçar Bode"; set .@exp,30000; break;
		case 10117: set .@name$,"Caçar Harpia"; set .@exp,30000; break;
		case 10118: set .@name$,"Caçar Relógio"; set .@exp,25000; break;
		case 10119: set .@name$,"Caçar Punk"; set .@exp,25000; break;
		case 10120: set .@name$,"Caçar Rideword"; set .@exp,25000; break;
		case 10121: set .@name$,"Caçar Kikimora"; set .@exp,30000; break;
		case 10122: set .@name$,"Caçar Boneca de Miyabi"; set .@exp,30000; break;
		case 10123: set .@name$,"Caçar Mi Gao"; set .@exp,30000; break;
		case 5055: set .@name$,"Caçar Mula Sem Cabeça"; set .@exp,30000; break;
		case 5056: set .@name$,"Caçar Tamruan"; set .@exp,30000; break;
		case 5057: set .@name$,"Coletar Máscaras do Festival para o Festival"; set .@exp,30000; break;
	}
	if (getargcount() == 1) {
		mes "'"+.@name$+"' completada. Você quer entregá-la e receber sua recompensa?";
		next;
		if(select("Entregar missão.:Cancelar.") == 1) {
			if (.@quest == 5057) delitem 7187,30; //Festival_Mask
			erasequest .@quest;
			getexp .@exp,0;
			mes "Você recebeu EXP por terminar a missão.";
			next;
			return;
		}
		close;
	} else {
		if (.@quest == 5057) {
			// Quest 5057 is the only collection quest, so separate conditions are unnecessary.
			if (checkquest(5057) == -1)
				set .@hunting,-1;
			else if (countitem(7187) >= 30)
				set .@hunting,2;
		} else
			set .@hunting, checkquest(.@quest,HUNTING);
		if (.@hunting == 0 || .@hunting == 1) {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "Volte aqui quando terminar a missão.";
		} else if (.@hunting == 2) {
			mes "Você completou a missão.";
			next;
			if(select("Entregar missão.:Cancelar.") == 1) {
				if (.@quest == 5057) delitem 7187,30; //Festival_Mask
				erasequest .@quest;
				getexp .@exp,0;
				mes "Você recebeu EXP por terminar a missão.";
			}
		}/* else if (.@hunting == 3) {
//			recall_completequest .@quest
			erasequest .@quest;
			mes "You have re-accepted the quest '"+.@name$+"'.";
		}*/ else {
			mes getarg(1);
			mes getarg(2);
			next;
			mes "Você quer aceitar a missão?";
			next;
			if(select("Sim.:Não.") == 1) {
				mes "Você recebeu a missão '"+.@name$+"'. Veja as informações da sua missão para mais detalhes.";
				setquest .@quest;
			}
		}
		close;
	}
}
