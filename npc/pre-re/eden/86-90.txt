//===== rAthena Script =======================================
//= Eden Group Quests - Mission [86 - 90]
//===== By: ==================================================
//= Chilly
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 86 - 90.
//===== Additional Comments: =================================
//= 1.0 First Version. [Chilly]
//= 1.1 Some little improvements here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Updated to match the official script. [Euphy]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,48,175,3	script	86-90 Mission Board	857,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "Espere um minuto!";
		mes "-Você precisa ter uma-";
		mes "-^4d4dff'Marca do Grupo do Éden'^000000-";
		mes "-para receber estas missões.-";
		next;
		switch(select("Apenas vá embora.:Missões do ^0000FFGrupo do Éden^000000?:Como posso ser um membro do ^0000FFGrupo do Éden^000000?")) {
		case 1:
			close;
		case 2:
			mes "::As Missões do Grupo do Éden";
			mes "::são basicamente missões";
			mes "::solicitadas por";
			mes "::pessoas de todo o reino.";
			mes "::Nós apenas as categorizamos.";
			next;
			mes "::No Grupo do Éden,";
			mes "::nós classificamos as missões";
			mes "::de acordo com o nível de dificuldade.";
			mes "::De acordo com este nível,";
			mes "::você pode encontrar";
			mes "::missões que";
			mes "::se adequam a você.";
			next;
			mes "::Quando você terminar,";
			mes "::você pode voltar";
			mes "::para o quadro de missões.";
			next;
			mes "::Então você será";
			mes "::recompensado!!";
			mes "::Simples, não é!!";
			mes "::Nenhuma comissão necessária!!";
			close;
		case 3:
			mes "::Você pode se tornar um membro";
			mes "::do Grupo do Éden";
			mes "::com ^0000FFLime Evenor^000000,";
			mes "::que está esperando no 1º andar.";
			next;
			mes "::Se você se tornar um membro,";
			mes "::você receberá uma";
			mes "::\"^006400Marca do Grupo do Éden^000000\".";
			mes "::Então você será capaz";
			mes "::de ver o quadro de missões.";
			close;
		}
	}
	if (BaseLevel < 86  || BaseLevel > 90) {
		// Quest IDs: 4167-4180
		// Quest cooldown IDs: 4198-4211
		setarray .@names$[0],
			"Libélula","Coelho da Primavera","Peste",
			"Bathory","Alarme",
			"Baba Yaga","Yao Jun","Soldado Atirador","Urso de Zíper",
			"Pequeno Dragão","Rafflesia","Venomous","Mineiro","Novus Amarelo";
		setarray .@exp[0],
			76000,82000,82000,
			72000,76000,
			72000,78000,82000,80000,
			72000,72000,72000,86000,80000;
		mes "Você deve estar entre o nível 86-90 para aceitar estas missões.";
		for(set .@quest,4167; .@quest<=4180; set .@quest,.@quest+1) {
			set .@hunting, checkquest(.@quest,HUNTING);
			if (.@hunting == 0 || .@hunting == 1) {
				next;
				mes ":: Você não pode prosseguir na";
				mes ":: Caça a ^0000FF"+.@names$[.@quest-4167]+"^000000.";
				mes ":: O registro para a missão";
				mes ":: foi cancelado.";
				erasequest .@quest;
				close;
			} else if (.@hunting == 2) {
				next;
				mes ":: Você adicionou informações";
				mes ":: sobre a missão";
				mes ":: Caça a ^0000FF"+.@names$[.@quest-4167]+"^000000";
				mes ":: no quadro de missões.";
				mes ":: Você teve uma";
				mes ":: experiência salutar.";
				erasequest .@quest;
				getexp .@exp[.@quest-4167],0;
				close;
			}
		}
		close;
	}
	mes "[Quadro de Missões 86-90]";
	mes "Existem vários pedidos neste quadro.";
	next;
	switch(select("^8B4513Ilha da Tartaruga^000000:^8B4513Calabouço da Torre do Relógio^000000:^8B4513Cidades de Expansão^000000:^8B4513Outras Regiões^000000")) {
	case 1:
		mes "^8B4513Ilha da Tartaruga^000000.";
		mes "Qual monstro você vai caçar ou terminou de caçar?";
		next;
		switch(select("^0000FFLibélula^000000:^0000FFCoelho da Primavera^000000:^0000FFPeste^000000")) {
			case 1: callsub L_Quest,4167,"Libélula",76000,"?i","Existe um monstro que só pode ser encontrado na ^8B4513Ilha da Tartaruga^000000.","s";
			case 2: callsub L_Quest,4168,"Coelho da Primavera",82000,"i","Existe um monstro que só pode ser encontrado na ^8B4513Ilha da Tartaruga^000000.","s";
			case 3: callsub L_Quest,4169,"Peste",82000,"?Ti","Existe um monstro que só pode ser encontrado na ^8B4513Ilha da Tartaruga^000000.","s";
		}
	case 2:
		mes "^8B4513Calabouço da Torre do Relógio^000000.";
		mes "Qual monstro você vai caçar ou terminou de caçar?";
		next;
		switch(select("^0000FFBathory^000000:^0000FFAlarme^000000")) {
			case 1: callsub L_Quest,4170,"Bathory",72000,"??i","O Calabouço da Torre do Relógio está cheio de monstros perigosos.";
			case 2: callsub L_Quest,4171,"Alarme",76000,"?i","O Calabouço da Torre do Relógio está cheio de monstros perigosos.";
		}
	case 3:
		mes "^8B4513Cidades de Expansão^000000";
		mes "Qual monstro você vai caçar ou terminou de caçar?";
		next;
		switch(select("^0000FFBaba Yaga^000000:^0000FFYao Jun^000000:^0000FFSoldado Atirador^000000:^0000FFUrso de Zíper^000000")) {
			case 1: callsub L_Quest,4172,"Baba Yaga",72000,"","A bruxa Baba Yaga pode ser encontrada nos arredores de Moscovia.";
			case 2: callsub L_Quest,4173,"Yao Jun",117000,"i","Existem monstros semelhantes a humanos que vagam pelo calabouço em Luoyang.";
			case 3: callsub L_Quest,4174,"Soldado Atirador",82000,"?i","Existem monstros semelhantes a humanos que vagam pelo calabouço em Amatsu.","s";
			case 4: callsub L_Quest,4175,"Urso de Zíper",120000,"?i","Existem monstros ursos infestando o calabouço de Kunlun.";
		}
	case 4:
		mes "[Quadro de Missões 86-90]";
		mes "Aqui está uma lista de pedidos aleatórios para caçar monstros em outras regiões de Midgard.";
		mes "Qual monstro você vai caçar ou terminou de caçar?";
		next;
		switch(select("^0000FFPequeno Dragão^000000:^0000FFRafflesia^000000:^0000FFVenomous^000000:^0000FFMineiro^000000:^0000FFNovus Amarelo^000000")) {
			case 1: callsub L_Quest,4176,"Pequeno Dragão",72000,"?i","Existem monstros do tipo dragão infestando a região oeste de Geffen.";
			case 2: callsub L_Quest,4177,"Rafflesia",72000,"÷þi","Existem monstros do tipo planta infestando os campos de Lighthalzen.";
			case 3: callsub L_Quest,4178,"Venomous",72000,"?","Existem monstros sem forma flutuando pelos campos de Einbroch.";
			case 4: callsub L_Quest,4179,"Mineiro",86000,"?","Existem monstros mortos-vivos empurrando um carrinho pelos campos e calabouços de Einbroch.";
			case 5: callsub L_Quest,4180,"Novus Amarelo",80000,"?","Existem monstros do tipo dragão amarelo aterrorizando os campos de Hugel.";
		}
	}
	end;

//callsub L_Quest,<quest ID>,"<monster name>",<reward EXP>,"<cutin image>","<description>"{,"s"};
L_Quest:
	set .@quest1, getarg(0);
	set .@quest2, .@quest1+31;
	.@cutin$ = getarg(3);
	set .@playtime, checkquest(.@quest2,PLAYTIME);
	if (.@playtime == 0 || .@playtime == 1) {
		mes "[Quadro de Missões 86-90]";
		mes "Você precisa esperar 3 horas antes de poder aceitar esta missão novamente.";
		close;
	}
	set .@hunting, checkquest(.@quest1,HUNTING);
	if (.@hunting == 0 || .@hunting == 1) {
		mes "[Quadro de Missões 86-90]";
		mes "Você já terminou de caçar todos os 30 "+getarg(1)+getarg(5,"")+"?";
		close;
	} else if (.@hunting == 2) {
		mes "[Quadro de Missões 86-90]";
		mes "Você completou a caçada.";
		mes "Por favor, aceite esta recompensa como compensação.";
		erasequest .@quest1;
		if (.@playtime > -1) erasequest .@quest2;
		setquest .@quest2;
		getexp getarg(2),0;
		close;
	}/* else if (.@hunting == 3) {
//		recall_completequest .@quest1
		erasequest .@quest1;
		mes "[Quadro de Missões 86-90]";
		mes "Você reaceitou a missão de Caça a "+getarg(1)+".";
		close;
	}*/ else {
		mes "[Quadro de Missões 86-90]";
		mes getarg(4);
		next;
		if (.@cutin$ != "") cutin .@cutin$,3;
		mes "[Quadro de Missões 86-90]";
		mes "Cace 30 monstros "+getarg(1)+" lá e poste seu sucesso neste quadro.";
		next;
		if (.@cutin$ != "") cutin .@cutin$,255;
		if(select("Eu vou caçá-los.:Não, obrigado.") == 1) {
			mes "[Quadro de Missões 86-90]";
			mes "Quando você tiver completado a missão, poste-a neste quadro e receba sua recompensa.";
			if (.@playtime > -1) erasequest .@quest2;
			setquest .@quest1;
		}
		close;
	}
}
