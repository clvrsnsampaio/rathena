//===== rAthena Script =======================================
//= Eden Group Quests - Mission [11 - 25]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.5
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 11 - 25.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//= 1.3 Bug fixes. [Capuche]
//= 1.4 Added support to Para_Team_Mark_ in Eden Group. [Ragno]
//= 1.5 Added use of F_HasEdenGroupMark function. [Ragno]
//============================================================

moc_para01,36,38,3	script	Mission [11 - 25]#Tuto	4_BOARD3,{
	if (!callfunc("F_HasEdenGroupMark")) {
		mes "- Você precisa ter uma -";
		mes "- ^4d4dff'Marca do Grupo do Éden'^000000 -";
		mes "- para receber estas missões. -";
		close;
	}
	for ( .@i = 11124; .@i < 11134; .@i++ )
		if (checkquest(.@i,PLAYTIME) == 2) erasequest .@i;
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			mes " - Você deve coletar sua - ";
			mes " - recompensa antes de iniciar - ";
			mes " - uma nova missão. - ";
			mes " ";
			mes " - Gerente: Spike - ";
			close;
		}
		else if (.@j >= 0) {
			mes " - Você só pode solicitar - ";
			mes " - uma missão por vez. - ";
			mes " ";
			mes " - Gerente: Spike - ";
			close;
		}
	}
	if (BaseLevel < 11) {
		mes " - Não há tarefas - ";
		mes " - disponíveis para você agora. - ";
		close;
	}
	if (BaseLevel > 25) {
		mes " - Estas missões são muito - ";
		mes " - fáceis para o seu nível - ";
		mes " - procure por uma missão - ";
		mes " - mais adequada ao seu nível. - ";
		close;
	}
	mes " - Quadro de missões -";
	mes " - para iniciantes. -";
	mes " ";
	mes " - Gerente: Spike -";
	next;
	switch(select("Caça aos Zangões:Caça aos Condores:Perna de Gafanhoto:Caça aos Rabos de Verme:Caça aos Esporos:Controle de Pragas:Caça aos Mukas:Caça aos Familiares:Coletar Penas:Coletar Esporos Venenosos:Cancelar")) {
		case 1: callsub L_Quest,11114,"Caça aos Zangões"," - Cace 10 ^4d4dff'Zangões'^000000 - "," - a noroeste de Prontera. - ";
		case 2: callsub L_Quest,11115,"Caça aos Condores"," - Cace 20 ^4d4dff'Condores'^000000 - "," - ao sul de Morroc. - ";
		case 3: callsub L_Quest,11116,"Perna de Gafanhoto"," - Cace 10 ^4d4dff'Rockers'^000000 - "," - a sudoeste de Prontera e - "," - traga 10 ^4d4dffPernas de Gafanhoto^000000. - ";
		case 4: callsub L_Quest,11117,"Caça aos Rabos de Verme"," - Cace 20 ^4d4dff'Rabos de Verme'^000000 - "," - a sudeste de Payon.- ";
		case 5: callsub L_Quest,11118,"Caça aos Esporos"," - Cace 30 ^4d4dff'Esporos'^000000 - "," - nos arredores de Payon. - ";
		case 6: callsub L_Quest,11119,"Controle de Pragas"," - Cace 20 ^4d4dffOvos de Besouro-Ladrão^000000 - "," - no Esgoto de Prontera - "," - e traga 10 ^4d4dffCrisálidas^000000. - ";
		case 7: callsub L_Quest,11120,"Caça aos Mukas"," - Cace 20 ^4d4dff'Mukas'^000000 - "," - ao sul de Prontera. - ";
		case 8: callsub L_Quest,11121,"Caça aos Familiares"," - Cace 20 ^4d4dffFamiliares^000000 - "," - nos Esgotos - "," - a oeste de Prontera. - ";
		case 9: callsub L_Quest,11122,"Coletar Penas"," - Cace 30 ^4d4dff'Peco Pecos'^000000 - "," - ao sul de Prontera. - ";
		case 10: callsub L_Quest,11123,"Coletar Esporos Venenosos"," - Cace 30 ^4d4dffEsporos Venenosos^000000 - "," - dentro da Torre de Geffen e - "," - traga 5 ^4d4dffEsporos Venenosos^000000. - ";
		case 11:
			mes " - Parar de ler o quadro de avisos. - ";
			close;
	}

L_Quest:
	.@quest_status = checkquest(getarg(0)+10,PLAYTIME);
	if (.@quest_status == 0 || .@quest_status == 1) {
		mes " - A missão ^4d4dff"+ getarg(1) +"^000000 - ";
		mes " - já foi concluída hoje. - ";
		mes " ";
		mes " - Gerente: Spike - ";
		close;
	}
	mes " - Missão '"+ getarg(1) +"' - ";
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	mes " ";
	mes " - Gerente: Spike - ";
	next;
	if (select("Eu farei esta missão.:Procurar outras missões.") == 2) {
		mes " - Vamos procurar por -";
		mes " - outras missões. - ";
		close;
	}
	for ( .@i = 2; .@i < getargcount(); .@i++ )
		mes getarg(.@i);
	setquest getarg(0);
	close;
}

moc_para01,32,30,6	script	Spike	4_F_CHILD,{
	for( .@i = 11114; .@i < 11124; .@i++ ) {
		.@j = checkquest(.@i,HUNTING);
		if (.@j == 2) {
			switch(.@i) {
				case 11114: callsub L_Quest,.@i,"Caça aos Zangões",900;
				case 11115: callsub L_Quest,.@i,"Caça aos Condores",1800;
				case 11116: callsub L_Quest,.@i,"Perna de Gafanhoto",1400,940,10;
				case 11117: callsub L_Quest,.@i,"Caça aos Rabos de Verme",2600;
				case 11118: callsub L_Quest,.@i,"Caça aos Esporos",3900;
				case 11119: callsub L_Quest,.@i,"Controle de Pragas",2900,915,10;
				case 11120: callsub L_Quest,.@i,"Caça aos Mukas",3200;
				case 11121: callsub L_Quest,.@i,"Caça aos Familiares",3200;
				case 11122: callsub L_Quest,.@i,"Coletar Penas",5400;
				case 11123: callsub L_Quest,.@i,"Coletar Esporos Venenosos",6000,7033,5;
			}
		}
		else if (.@j >= 0) {
			mes "[Spike]";
			mes "Você ainda não terminou sua missão.";
			mes "Continue o bom trabalho. Você está quase terminando.";
			close;
		}
	}
	mes "[Spike]";
	mes "Você está procurando trabalho?";
	mes "Nós sempre temos muito trabalho a fazer.";
	next;
	mes "[Spike]";
	mes "Por que você não dá uma olhada e encontra algumas tarefas interessantes?";
	close;

L_Quest:
	.@quest_id = getarg(0);
	if (checkquest(.@quest_id,HUNTING) == 2) {
		.@item_req = getarg(3,0);
		.@req_amount = getarg(4,0);
		if (.@item_req) {
			if (countitem(.@item_req) < .@req_amount) {
				mes "[Spike]";
				mes "Você não tem "+ getitemname(.@item_req) +" suficiente.";
				mes "Você precisa trazer "+ .@req_amount +" "+ getitemname(.@item_req) +".";
				close;
			}
		}
		mes "[Spike]";
		mes "Hmm... deixe-me ver~";
		mes "Você completou a missão '"+ getarg(1) +"'.";
		next;
		mes "[Spike]";
		mes "Bom trabalho~!!";
		mes "Nosso cliente está muito feliz com seu trabalho. Ele apreciará o que você fez.";
		mes "Por favor, nos ajude novamente. Obrigado!";
		if (.@item_req)
			delitem .@item_req,.@req_amount;
		erasequest .@quest_id;
		setquest .@quest_id+10;
		getexp getarg(2),0;
		set #CASHPOINTS, #CASHPOINTS + 1;
		close;
	}
}
